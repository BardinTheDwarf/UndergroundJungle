plugins {
    id 'idea'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '[6.0,6.2)'
}

base {
    version = '2.1-forge'
    group = 'io.github.hexagonnico.undergroundjungle'
    archivesName = "undergroundjungle-1.20"
}

minecraft {
    mappings channel: 'official', version: '1.20.1'

    runs {
        client {
            workingDirectory this.file('../run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', 'underground_jungle'
            mods {
                undergroundjungle {
                    source this.sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources.srcDir 'src/generated/resources'

dependencies {
    minecraft "net.minecraftforge:forge:1.20.1-47.2.0"
    compileOnly project(":common-1.20")
}

tasks.withType(JavaCompile).configureEach {
    source project(":common").sourceSets.main.allSource
    source project(":common-1.20").sourceSets.main.allSource
}

processResources {
    from project(":common").sourceSets.main.resources
    from project(":common-1.20").sourceSets.main.resources
}

jar {
    manifest {
        attributes([
            "Specification-Title": 'underground_jungle',
            "Specification-Vendor": 'HexagonNico',
            "Specification-Version": project.version,
            "Implementation-Title": project.name,
            "Implementation-Version": project.jar.archiveVersion,
            "Implementation-Vendor": 'HexagonNico',
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

jar.finalizedBy('reobfJar')
