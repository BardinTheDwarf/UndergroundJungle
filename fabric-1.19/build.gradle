plugins {
    id 'java'
    id 'idea'
    id 'fabric-loom' version '0.12-SNAPSHOT'
}

base {
    version = '1.1.3-fabric'
    group = 'io.github.hexagonnico.undergroundjungle'
    archivesName = "undergroundjungle-1.19"
}

java {
    toolchain {
        JavaLanguageVersion.of(17)
    }
}

repositories {
    maven {
        url = 'https://maven.minecraftforge.net/'
    }
    maven {
        url "https://cursemaven.com"
    }
}

dependencies {
    minecraft "com.mojang:minecraft:1.19.4"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:0.14.23"
    modImplementation "net.fabricmc.fabric-api:fabric-api:0.87.1+1.19.4"
    implementation project(":common-1.19")
    implementation project(":common")
    // Optional TerraBlender dependency
    modImplementation 'com.github.glitchfiend:TerraBlender-fabric:1.19.4-2.3.0.22'
    modRuntimeOnly "curse.maven:terralith-513688:4823707"
}

loom {
    mixin {
        defaultRefmapName.set "mixin.undergroundjungle.refmap.json"
    }
    runs {
        client {
            client()
            setConfigName("fabric-1.19")
            ideConfigGenerated(true)
            runDir("../run")
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    source project(":common").sourceSets.main.allSource
    source project(":common-1.19").sourceSets.main.allSource
}

processResources {
    from project(":common").sourceSets.main.resources
    from project(":common-1.19").sourceSets.main.resources
}

jar {
    manifest {
        attributes([
            "Specification-Title": 'underground_jungle',
            "Specification-Vendor": 'HexagonNico',
            "Specification-Version": project.version,
            "Implementation-Title": project.name,
            "Implementation-Version": project.jar.archiveVersion,
            "Implementation-Vendor": 'HexagonNico',
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}
